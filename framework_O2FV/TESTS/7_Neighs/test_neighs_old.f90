program test_neighs

use frmwork_space3d
use dholder_impdefs
use frmwork_grid
use frmwork_gridmaker
use mpiO2
use utilmod_tecplot
use frmwork_oofv
use frmwork_oofvmpi
use frmwork_lassos

implicit none

type(point) :: ps,pe

! nodes, faces and fvs are already defined in framework_oofv
real(kind(0.d0)) , dimension(:), allocatable, target :: field_pc
integer :: i, nx, ny, nz, myunit, id_cell

call initialize_mpiO2(.false.)

! grid setup

nx = 12
ny = 12
nz = 12

ps = point(-1d0,-1d0,-1d0)
pe = point(1d0,1d0,1d0)

if (parallel_execution) call partitions_x(nx,ps,pe)

allocate(nodes(size_nodes_cartesian(nx,ny,nz)),faces(size_faces_cartesian(nx,ny,nz)),fvs(size_fvs_cartesian(nx,ny,nz)))

call cartesian_grid(nx,ny,nz,ps,pe,nodes,faces,fvs)

call associate_pointers(nodes,faces,fvs)

call faces%metrics
call fvs%metrics

call mpi_boundary%link(faces)

call mpi_boundary%update

tot_vars = maxval(faces%ivar)

! find neighborhoods
!  
!           call neighs_setup(lasso,lvl_max,tags)
! 
!  The neighborhood setup function finds the neighboring elements of cells. The cells whose neighborhood is found
!  is every cell in the FV array. If the optional logical array tags is given then it only finds neighborhoods for 
!  cells that tag is true. The neighborhoods are generated by following the grids graph lvl max times. To stress 
!  that the default option is to follow the graph the first argument for default use must be follow_graph and 
!  although the lvl_max is optional in that case it must be given, since there is no restrain for creating the 
!  neighborhood. The neighborhood constrains are called lasso: 
! 
!   lasso :   follow_graph -> no constrain 
!             
!             box          -> find all cells in a box whose half-length is defined by set_box_halflength
!             
!             Vbox         -> find all cells in a box whose half-length is defined automatically by the local cell volume
!             
!             ball         -> find all cells in a ball whose radius is defined by set_ball_radius
!             
!             Vball        -> find all cells in a ball whose radius is defined automatically by the local cell volume
!             
!             elements_no  -> stop adding neighborhood levels when the  
!             

! Uncomment one of the call commands below to find the neighborhood with the given lasso.
! If you leave two uncommented the only the command executed last is saved
!call neighs_setup(follow_graph,2,dbg=.true.)
!!call set_cells_extends(2)
call neighs_setup_serial(Vbox,dbg=.true.)
!call neighs_setup(Vball,dbg=.true.)

! visualize a cell neighborhood in matlab
!id_cell=325 ! -> boundary cell
id_cell=631

open(12,file='cell_neigh.m')

! write the cell
call FVs(id_cell)%write(12,id_cell,'y')

! write the cell's in the neighborhood
do i=1,size(FVs(id_cell)%neighs)
    
    call FVs(FVs(id_cell)%neighs(i))%write(12,FVs(id_cell)%neighs(i),'b')
    
end do

close(12)

! to find the centers of the cells in the neighborhood of the 
! cell i, you may either use:
! 
!             FVs(FVs(i)%neighs)%pc
!              
! or through the function
!  
!             FVs(i)%neighs_pc()
! 
! To store the points in an array, say neigh_cells_center:
!
!       allocate(neigh_cells_center,source=FVs(FVs(i)%neighs)%pc)
! 
! or
! 
!       allocate(neigh_cells_center,source=FVs(i)%neighs_pc())
! 
! Note however, that the neighs_pc function is more general since it 
! is used in the same way for the parallel extensions.

open(12,file='cell_neigh_points.txt')

id_cell=631

! cell's center
write(12,*), '---'
write(12,*), FVs(id_cell)%pc
write(12,*), '---'

! write the centers of the cells in the neighborhood
write(12,*), FVs(id_cell)%neighs_pc()


close(12)



! we can also visualize the neighborhood in tecplot
! we create a field which is zero everywhere except the cell neighborhood
! where the field is one. We may remove the other cells with tecplot blanking
! menu. Turn on the mesh, go at -> Plot -> Blanking -> Value blanking:
!              include blanking        : on
!              active                  : on
!              blank entire cells when : primary cell value
!              blank when              : scalar1

allocate(field_pc(size(FVs)),source=0d0)

field_pc(FVs(id_cell)%neighs)=1d0

call create_tecplot_files(1)

call tecplot(1)%set(nodes,faces,fvs,mpi_boundary)

call tecplot(1)%plot(field_pc)

call tecplot(1)%update

call tecplot(1)%close

call finalize_mpiO2(.false.)

end program test_neighs